/*
 * Copyright 2015 Real Logic Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

defaultTasks 'clean', 'build'

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

// assume that SBE is built in a nearby directory
FileCollection sbeJar = files('../simple-binary-encoding/build/libs/sbe-all-1.1-RC2-SNAPSHOT.jar')

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile 'uk.co.real-logic:Agrona:0.3.2-SNAPSHOT',
            'uk.co.real-logic:aeron-common:0.1-SNAPSHOT',
            'uk.co.real-logic:aeron-client:0.1-SNAPSHOT',
            'uk.co.real-logic:aeron-driver:0.1-SNAPSHOT',
            'uk.co.real-logic:sbe:1.1-RC2-SNAPSHOT'
    }

    build.doLast {
        tasks.shadowJar.execute()
    }

    task(generateCodecs, type: JavaExec) {
        description = "Generate SBE codecs into ${project.projectDir}/src/main/java/amaethon/generated"
        main = 'uk.co.real_logic.sbe.SbeTool'
        classpath = sbeJar
        systemProperties(
            'sbe.output.dir': "${project.projectDir}/src/main/java",
            'sbe.target.language': 'Java',
            'sbe.validation.stop.on.error': 'true',
            'sbe.validation.xsd': "${project.projectDir}/src/main/resources/fpl/SimpleBinary1-0.xsd")
        args "${project.projectDir}/src/main/resources/Amaethon-schema.xml"
    }

    task(run, type: JavaExec) {
        description = "Run exercise"
        main = 'amaethon.RunExercise'
        classpath = sourceSets.main.runtimeClasspath
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
